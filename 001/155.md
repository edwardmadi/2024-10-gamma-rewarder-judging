Ancient Slate Hippo

High

# Changing `blocksPerEpoch` will brick current distributions

### Summary

Modifying `blocksPerEpoch` will make it impossible to claim previously created distributions

### Root Cause

`blocksPerEpoch` is used as a global variable to determine the amount of blocks in each epoch. When a new distribution is created, its duration must be a multiple of `blocksPerEpoch`:

```solidity

function createDistribution(
        address _hypervisor, 
        address _rewardToken, 
        uint256 _amount, 
        uint64 _startBlockNum, 
        uint64 _endBlockNum
    ) external nonReentrant {
        require(_hypervisor != address(0), "Zero address not allowed"); // @audit Hypervisor can be **ANY** address, even if it does not belong to gamma.
        ...
        require((_endBlockNum - _startBlockNum) % blocksPerEpoch == 0, "Distribution length must be multiples of blocks per epoch");

   ...
```

When claiming a distribution in [GammaRewarder.sol#L202](https://github.com/sherlock-audit/2024-10-gamma-rewarder/blob/main/GammaRewarder/contracts/GammaRewarder.sol#L202), it is also enforced that the difference between `endBlock` and `startBlock` is a multiple of the current `blocksPerEpoch`:

```solidity

 function handleProofResult(bytes32, bytes32 _vkHash, bytes calldata _appCircuitOutput) internal override {
        ...
  
        (
            address userAddress,
            address lpTokenAddress,
            uint64 startBlock,
            uint64 endBlock,
            bytes32 distributionId,
            address rewardTokenAddress,
            uint248 distributionAmountPerEpoch,
            uint248 totalRewardAmount
        ) = decodeOutput(_appCircuitOutput);

       ...
        require(startBlock < endBlock && (endBlock - startBlock) % blocksPerEpoch == 0, "Claim period must be valid");
        ...

```
Because `blocksPerEpoch` is used globally, instead of being tracked  per distribution, if the `blocksPerEpoch` is changed between a distribution creation and its claim, the check performed in `handleProofResult` could fail, making it impossible to claim the distribution.


### Internal pre-conditions

1. A distribution is created. At this point, `blocksPerEpoch` has a certain value. From the examples in the README, let's say that the value is set to 6 hours, so it is 10800 (6hrs * 60mins * 60 seconds / 2-second block time in Optimism). To make it simple, let's say it is a distribution with only one epoch, having start block of 0, and end block of 10800 (so (10800 - 0) % 10800 == 0).

### External pre-conditions

None.

### Attack Path

1. The admin decides to change `blocksPerEpoch` from 6 hours to 5 hours, so it is now decreased from 10800 to  9000 (5hrs * 60mins * 60 seconds / 2-second block time in Optimism).
2. Once the distribution can be claimed, user triggers the claiming flow and `handleProofResult` is called. However, the function will revert, because **the difference between start and end block is not multiple of the currently configured `blocksPerEpoch` (so (10800 - 0) % 9000 != 0)**.

### Impact

High. Funds transferred in the contract will remain locked until a correct `blocksPerEpoch` is configured again, if ever configured. However, changing `blocksPerEpoch` again to allow previous distributions to be claimed will also DoS distributions created with the new `blocksPerEpoch` value. It is also worth noting that the README mentions that any value in the 6 hours - 1 day worth of blocks would be reasonable to be configured.

### PoC

_No response_

### Mitigation

Store the currently configured `blocksPerEpoch` when creating a distribution.

```diff
// File: GammaRewarder.sol
struct DistributionParameters {
    // ID of the distribution (populated once created)
    bytes32 distributionId;
    // Address of the Gamma Hypervisor that needs to be incentivized
    address hypervisor;
    // Address of the token of distribution
    address rewardToken;
    // The distribution amount per epoch
    uint256 distributionAmountPerEpoch;
    // Block number at where the distribution should start
    uint64 startBlockNumber;
    // Block number at where the distribution should end
    uint64 endBlockNumber;
    // Wallet address of incentivizor who creates this distribution
    address incentivizor;
+  uint64 blocksPerEpoch;
}

function createDistribution(
        address _hypervisor, 
        address _rewardToken, 
        uint256 _amount, 
        uint64 _startBlockNum, 
        uint64 _endBlockNum
    ) external nonReentrant {
        ...
        DistributionParameters memory newDistribution = DistributionParameters({
            distributionId: distributionId,
            hypervisor: _hypervisor,
            rewardToken: _rewardToken,
            distributionAmountPerEpoch: amountPerEpoch,
            startBlockNumber: _startBlockNum,
            endBlockNumber: _endBlockNum,
            incentivizor: msg.sender,
+         blocksPerEpoch: blocksPerEpoch
        });

```

Then, use such value in the requirement when claiming:

```diff

function handleProofResult(bytes32, bytes32 _vkHash, bytes calldata _appCircuitOutput) internal override {
        require(vkHashes[_vkHash], "invalid vk");
  
        (
            address userAddress,
            address lpTokenAddress,
            uint64 startBlock,
            uint64 endBlock,
            bytes32 distributionId,
            address rewardTokenAddress,
            uint248 distributionAmountPerEpoch,
            uint248 totalRewardAmount
        ) = decodeOutput(_appCircuitOutput);

        DistributionParameters memory params = distributions[distributionId];
-        require(startBlock < endBlock && (endBlock - startBlock) % blocksPerEpoch == 0, "Claim period must be valid");
+        require(startBlock < endBlock && (endBlock - startBlock) % params.blocksPerEpoch == 0, "Claim period must be valid");
```
