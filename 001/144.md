Basic Watermelon Bison

High

# Users will not be able to claim token rewards if the  `distributionAmountPerEpoch`  is very large

### Summary

In `GammaRewarder.sol` the `handleProofResult()` function defines `distributionAmountPerEpoch` as a uint248 after decoding it from the `_appCircuitOutput` and then it requires `distributionAmountPerEpoch == params.distributionAmountPerEpoch` but when a `Distribution` is created `DistributionParameters.distributionAmountPerEpoch` is a uint256 therefore `param.distributionAmountPerEpoch` is of uint256 type which would cause any `amountPerEpoch` >= 2**248 to always fail during claim by  users even if they are eligible for rewards due to an overflow in `distributionAmountPerEpoch` in `handleProofResult()` since it's of type uint248.

### Root Cause

-In `GammaRewarder.sol:232` distributionAmountPerEpoch is being returned as a uint248. https://github.com/sherlock-audit/2024-10-gamma-rewarder/blob/main/GammaRewarder/contracts/GammaRewarder.sol#L232


-In `GammaRewarder.sol:197` distributionAmountPerEpoch is defined as type uint248. https://github.com/sherlock-audit/2024-10-gamma-rewarder/blob/main/GammaRewarder/contracts/GammaRewarder.sol#L197

-While In `GammaRewarder.sol:20` distributionAmountPerEpoch is originally defined as uint256 type in the `DistributionParameters` struct. https://github.com/sherlock-audit/2024-10-gamma-rewarder/blob/main/GammaRewarder/contracts/GammaRewarder.sol#L20

### Internal pre-conditions

1. Incentivizor needs to set an  `_amount` greater or equal to 2**248 when calling `createDistribution()`.
2. After `uint256 amountPerEpoch = realAmountToDistribute / ((_endBlockNum - _startBlockNum) / blocksPerEpoch);` has been calculated, `amountPerEpoch` needs to be  greater or equal to 2**248 eg: if  `realAmountToDistribute` = 2**248 && `(_endBlockNum -  _startBlockNum)` = 200  && `blocksPerEpoch` = 200. Simplest way would be making  `_startBlockNum` and `_endBlockNum` have values in such a way that `_endBlockNum - _startBlockNum == blocksPerEpoch`.

### External pre-conditions

1. `_rewardToken` in `createDistribution()` has to be a whitelisted erc-20 standard token with a very high supply ( Ts ) ( ie: 2**248 >= Ts <= 2**256 - 1).


### Attack Path

1. A token with a very large supply gets whitelisted `>= 2** 248 `.
2. The Incentivizor creates a distribution with `createDistribution()` and that token passed as the `_rewardToken` and `_amount` >= 2** 248.
3. Incentivizor sets _startBlockNum and _endBlockNum must be given values in such a way that `amountPerEpoch` is also `>= 2**248`.
4. Users try to claim by calling `handleProofResult()` but it keeps reverting due to this condition `require(lpTokenAddress == params.hypervisor && rewardTokenAddress == params.rewardToken && distributionAmountPerEpoch == params.distributionAmountPerEpoch, "Distribution params must match");` since `distributionAmountPerEpoch` will not be equal to `params.distributionAmountPerEpoch` due to the `uint248` overflow of  `distributionAmountPerEpoch`.

### Impact

The entire reward tokens are locked in the contract perpetually (permanently lost funds).

### PoC

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

//This is a stripped down version from the original contract to show PoC

import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {SafeERC20} from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";

contract Distribution {
    using SafeERC20 for IERC20;

    struct DistributionParameters {
        bytes32 distributionId;
        uint256 distributionAmountPerEpoch; 
        uint64 startBlockNumber;
        uint64 endBlockNumber;
    }

    mapping(bytes32 => DistributionParameters) public distributions;

    mapping(address => uint256) public nonces;

    uint64 public blocksPerEpoch = 200; // Hardcoded just for testing purpose

    uint256 public constant MAX_DISTRIBUTION_BLOCKS = 9_676_800;

    function createDistribution(
        uint256 _amount,
        //  _startBlockNum and _endBlockNum must be given values in such a way that _endBlockNum - _startBlockNum == 
        //blocksPerEpoch
        uint64 _startBlockNum, // eg: 0
        uint64 _endBlockNum // eg: 200
    ) public /*should be `external`, was made public to simplify testing */ returns (bytes32) {
        require(_endBlockNum > _startBlockNum, "Invalid block range");
        require(_startBlockNum > block.number, "Start block must be in the future");
        require((_endBlockNum - _startBlockNum) <= MAX_DISTRIBUTION_BLOCKS, "Duration exceeds max");
        require(_amount > 0, "Amount must be greater than zero");
        require(
            (_endBlockNum - _startBlockNum) % blocksPerEpoch == 0,
            "Must be multiple of blocks per epoch"
        );

        uint256 amountPerEpoch = _amount / ((_endBlockNum - _startBlockNum) / blocksPerEpoch);
        uint256 senderNonce = nonces[msg.sender];
        bytes32 distributionId = keccak256(abi.encodePacked(msg.sender, senderNonce));

        DistributionParameters memory newDistribution = DistributionParameters({
            distributionId: distributionId,
            distributionAmountPerEpoch: amountPerEpoch,
            startBlockNumber: _startBlockNum,
            endBlockNumber: _endBlockNum
        });

        distributions[distributionId] = newDistribution;
        nonces[msg.sender] = senderNonce + 1;
        return distributionId;
    }

    function handleProofResult(
        bytes32 distributionId,
        uint64 startBlock,
        uint64 endBlock
    ) public /*should be `external, was made public to simplify testing `*/ view {
        //mimicking getting output from _appCircuitOutput
        uint248 distributionAmountPerEpoch = getDistributionAmountPerEpoch(distributionId);
        // Retrieve the distribution parameters using the provided distribution ID
        DistributionParameters memory params = distributions[distributionId];

        require(startBlock < endBlock && (endBlock - startBlock) % blocksPerEpoch == 0, "Claim period must be valid");
        require(startBlock >= params.startBlockNumber && endBlock <= params.endBlockNumber, "Claim range must match distribution range.");
        require(
            /* lpTokenAddress == params.hypervisor && rewardTokenAddress == params.rewardToken && - omitted (not necessary for the PoC) */
            distributionAmountPerEpoch == params.distributionAmountPerEpoch, "Distribution params must match"
        );

    }

    function getDistributionAmountPerEpoch(bytes32 distributionId) public view returns (uint248) {
        return  uint248(distributions[distributionId].distributionAmountPerEpoch);
    }

    // Performs the entire test in one call and reverts which models how the main `handleProofResult()` in `GammaRewarder.sol`
    // would behave, hence making rewards unclaimable. 
    function test() external {
        uint256 amount = 2**248; // This exceeds uint248 max value
        uint64 startBlockNum = uint64(block.number + 1);
        uint64 endBlockNum = startBlockNum + 200;

        // Create a distribution
        bytes32 distributionId = createDistribution(amount, startBlockNum, endBlockNum);
        // Call handleProofResult with valid values
        handleProofResult(distributionId, startBlockNum, endBlockNum);
    }


}

```


### Mitigation

`distributionAmountPerEpoch` and `totalRewardAmount` should be made uint256 all through the contract .